shell grammarより抜粋

simple_command   : cmd_prefix cmd_word cmd_suffix
                 | cmd_prefix cmd_word
                 | cmd_prefix
                 | cmd_name cmd_suffix
                 | cmd_name
                 ;
cmd_name         : WORD                   /* Apply rule 7a */
                 ;
cmd_word         : WORD                   /* Apply rule 7b */
                 ;
cmd_prefix       :            io_redirect
                 | cmd_prefix io_redirect
                 |            ASSIGNMENT_WORD
                 | cmd_prefix ASSIGNMENT_WORD
                 ;
cmd_suffix       :            io_redirect
                 | cmd_suffix io_redirect
                 |            WORD
                 | cmd_suffix WORD
                 ;
io_redirect      :           io_file
                 | IO_NUMBER io_file
                 |           io_here
                 | IO_NUMBER io_here
                 ;
io_file          : '<'       filename
                 | LESSAND   filename
                 | '>'       filename
                 | GREATAND  filename
                 | DGREAT    filename
                 | LESSGREAT filename
                 | CLOBBER   filename
                 ;
filename         : WORD                      /* Apply rule 2 */
                 ;
io_here          : DLESS     here_end
                 | DLESSDASH here_end
                 ;
here_end         : WORD                      /* Apply rule 3 */

入門bashより抜粋

<redirect> : '>' word
           | '<' word
           | '>>' word
           | '<<' word

BNF記法 /* linebreakなし、リダイレクトは<&、>&、<>、>|なし*/

pipe_sequence ::= command
                | pipe_sequence '|' command

command ::= cmd_prefix cmd_word cmd_suffix
          | cmd_prefix cmd_word
          | cmd_prefix
          | cmd_name cmd_suffix
          | cmd_name

cmd_prefix ::= io_redirect
             | cmd_prefix io_redirect

cmd_suffix ::= io_redirect
             | cmd_suffix io_redirect
             | WORD
             | cmd_suffix WORD

io_redirect ::= io_file
              | IO_NUMBER io_file
              | io_here
              | IO_NUMBER io_here

io_file ::= '<' filename
          | '>' filename
          | '<<' filename
          | '>>' filename

io_here ::= '<<' here_end

EBNF記法:

pipe_sequence ::= command ('|' command)*

command ::= io_redirect+ (cmd_word cmd_suffix?)?
          | cmd_name cmd_suffix?

cmd_suffix ::= (io_redirect | WORD)+

io_redirect ::= IO_NUMBER? (io_file | io_here)

io_file ::= ('<' | '>' | '>>') filename

io_here ::= '<<' here_end

IO_NUMBERを実装しないEBNF記法:

pipe_sequence ::= command ('|' command)*
command ::= cmd_prefix (cmd_word cmd_suffix?)?
          | cmd_name cmd_suffix?
cmd_prefix ::= io_redirect+
cmd_suffix ::= (io_redirect | WORD)+
io_redirect ::= io_file | io_here
io_file ::= ('<' | '>' | '>>') filename
io_here ::= '<<' here_end